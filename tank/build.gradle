plugins {
    id 'java'
    id 'application'
    alias(libs.plugins.openjfx)
}

group 'org.bougainvilleas.tank'
version '0.0.1'

application {
    mainModule = 'org.bougainvilleas.tank'
    mainClass = 'org.bougainvilleas.tank.TankApp'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    maven {
        url 'https://maven.aliyun.com/repository/public/'
    }
    mavenCentral()
}
//openjdk 不带 javafx 需要引入 javafx 且 com.github.almasb 需要 exclude
javafx {
    version = libs.versions.jfx
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.media']
}

dependencies {

    // jdk 带 javafx 可以不需要 exclude
    implementation(libs.fxgl) {
        exclude group: 'org.openjfx', module: 'javafx-base'
        exclude group: 'org.openjfx', module: 'javafx-graphics'
    }

    testImplementation libs.junit5.api
    testRuntimeOnly libs.junit5.engine
}

test {
    useJUnitPlatform()
}
